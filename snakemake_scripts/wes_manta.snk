####
#### Manta
####

rule tabix_bedfile:
	input:
		BED = EXOME_BED
	output:
		bed_gz = OUTDIR + "/temp/exome_region.bed.gz",
		bed_tbi = OUTDIR + "/temp/exome_region.bed.gz.tbi"
	threads: 1
	shell:
		"""
		cp {EXOME_BED} {OUTDIR}/temp/exome_region.bed
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} /gpfs/fs0/scratch/n/nicholsa/zyfniu/singularity_images/nfcore-sarek-2.6.img \
			bgzip -c {OUTDIR}/temp/exome_region.bed > {output.bed_gz}
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} /gpfs/fs0/scratch/n/nicholsa/zyfniu/singularity_images/nfcore-sarek-2.6.img \
				tabix -p bed {output.bed_gz}
		"""

rule config_manta:
	input:
		normal = OUTDIR + "/Recal/{patient}-N.recal.bam",
		tumor = OUTDIR + "/Recal/{tumor}.recal.bam",
		index_t = OUTDIR + "/Recal/{tumor}.recal.bai",
		index_n = OUTDIR + "/Recal/{patient}-N.recal.bai",
		bed_gz = OUTDIR + "/temp/exome_region.bed.gz",
		bed_tbi = OUTDIR + "/temp/exome_region.bed.gz.tbi"
	output: temp(OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/runWorkflow.py")
	threads: 2
	shell:
		"""
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} /gpfs/fs0/scratch/n/nicholsa/zyfniu/singularity_images/nfcore-sarek-2.6.img \
		configManta.py \
		--normalBam {input.normal} \
		--tumorBam  {input.tumor} \
		--reference {REF_fasta} \
		--callRegions {input.bed_gz} \
		--exome \
		--runDir {OUTDIR}/temp/Manta/{wildcards.tumor}_vs_{wildcards.patient}-N
		"""

rule manta:
	input:
		normal = OUTDIR + "/Recal/{patient}-N.recal.bam",
		tumor = OUTDIR + "/Recal/{tumor}.recal.bam",
		script = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/runWorkflow.py",
		index_t = OUTDIR + "/Recal/{tumor}.recal.bai",
		index_n = OUTDIR + "/Recal/{patient}-N.recal.bai"
	output:
		sv = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/candidateSV.vcf.gz",
		smallindel = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/candidateSmallIndels.vcf.gz",
		diploidSV = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/diploidSV.vcf.gz",
		somaticSV = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/somaticSV.vcf.gz",
		svIndex = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/candidateSV.vcf.gz.tbi",
		smallindelIndex = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/candidateSmallIndels.vcf.gz.tbi",
		diploidSVIndex = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/diploidSV.vcf.gz.tbi",
		somaticSVIndex = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/somaticSV.vcf.gz.tbi"
	group: "manta"
	threads: 80
	shell:
		"""
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} /gpfs/fs0/scratch/n/nicholsa/zyfniu/singularity_images/nfcore-sarek-2.6.img \
		python {input.script} -m local -j {threads} -g 160
		touch {output}
		"""

rule mv_manta_files:
	input:
		file = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/{structure}.vcf.gz",
		index = OUTDIR + "/temp/Manta/{tumor}_vs_{patient}-N/results/variants/{structure}.vcf.gz.tbi"
	output:
		file = OUTDIR + "/results/Manta/{tumor}_vs_{patient}-N/Manta_{tumor}_vs_{patient}-N.{structure}.vcf.gz",
		index = OUTDIR + "/results/Manta/{tumor}_vs_{patient}-N/Manta_{tumor}_vs_{patient}-N.{structure}.vcf.gz.tbi"
	group: "manta"
	threads: 1
	shell:
		"""
		cp {input.file} {output.file}
		cp {input.index} {output.index}
		"""

rule annotate_manta:
	input:
		vcf = OUTDIR + "/results/Manta/{tumor}_vs_{patient}-N/Manta_{tumor}_vs_{patient}-N.{structure}.vcf.gz"
	output:
		annotatedvcf = temp(OUTDIR + "/results/Manta/{tumor}_vs_{patient}-N/Manta_snpeff_{tumor}_vs_{patient}-N.{structure}.ann.vcf")
	threads: 2
	group: "manta"
	shell:
		"""
		cd {OUTDIR}/results/Manta/{wildcards.tumor}_vs_{wildcards.patient}-N
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} $SCRATCH/singularity_images/nfcore-sareksnpeff-2.6.GRCh38.img \
		snpEff -Xmx8g \
		GRCh38.86 \
		-csvStats {wildcards.tumor}_{wildcards.structure}_snpEff.csv \
		-nodownload \
		-canon \
		{input} \
		> {output}
#		mv snpEff_summary.html {wildcards.tumor}_{wildcards.structure}_snpEff.html
		"""

rule zip_manta:
	input:
		annotatedvcf = OUTDIR + "/results/Manta/{tumor}_vs_{patient}-N/Manta_snpeff_{tumor}_vs_{patient}-N.{structure}.ann.vcf"
	output: annotatedvcf = OUTDIR + "/results/Manta/{tumor}_vs_{patient}-N/Manta_snpeff_{tumor}_vs_{patient}-N.{structure}.ann.vcf.gz"
	threads: 2
	group: "manta"
	shell:
		"""
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} \
			/gpfs/fs0/scratch/n/nicholsa/zyfniu/singularity_images/nfcore-sarek-2.6.img bgzip < {input} > {output}
		singularity exec -B $SCRATCH/igenomes_ref,{OUTDIR} \
			/gpfs/fs0/scratch/n/nicholsa/zyfniu/singularity_images/nfcore-sarek-2.6.img tabix {output}
		"""
